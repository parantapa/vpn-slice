# Maintainer: kmille aur@androidloves.me
# Contributer: Matt Parnell <parwok@gmail.com>

pkgname=vpn-slice-git
_pkgname=vpn-slice
pkgver=r234.c6d8afa
pkgrel=1
pkgdesc="Python based vpnc-script replacement for easy and secure split-tunnel VPN setup"
arch=('any')
url="https://github.com/parantapa/vpn-slice"
license=('GPL')
makedepends=(git python-setuptools)
depends=(python python-setproctitle python-dnspython bind-tools iproute2 iptables)
source=(git+https://github.com/parantapa/vpn-slice)
sha512sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build() {
  cd "$srcdir/$_pkgname"
  python setup.py build
}

package() {
  cd "$srcdir/$_pkgname"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
